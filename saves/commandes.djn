@section infos sur le jeu et sa licence.

-- >>############################################################<<
-- >># PARTIE 1 : INFOS SUR LE JEU ET SA LICENCE                #<<
-- >>############################################################<<

Le titre du jeu est "Commandes de base".
L’auteur du jeu est "Jonathan Claes".
La version du jeu est "0.8".
Le titre de la licence est "Creative Commons Attribution 4.0 International License".
Le lien de la licence est "https://creativecommons.org/licenses/by/4.0/".

@section ouvrir et fermer.
-- >>############################################################<<
-- >># OUVRIR ET FERMER                                         #<<
-- >>############################################################<<

-- ==========
--   OUVRIR
-- ==========

-- a) ouvrir ceci
ouvrir ceci est une action qui concerne un objet visible.

refuser ouvrir ceci:
  si ceci n'est pas ouvrable, dire "Ça ne s'ouvre pas.";
  si ceci est verrouillé, dire "C'est verrouillé.".
  
exécuter ouvrir ceci:
  changer ceci est ouvert;
  changer ceci n'est plus fermé.

terminer ouvrir ceci:
  dire "C'est ouvert.";
  si ceci est un contenant, dire "[contenu ceci]".
  
-- ==========
--   FERMER
-- ==========

-- a) fermer ceci
fermer ceci est une action qui concerne un objet visible.

refuser fermer ceci:
  si ceci n'est pas ouvrable, dire "Ça ne se ferme pas.";
  si ceci est verrouillé, dire "C'est verrouillé.".
  
exécuter fermer ceci:
  changer ceci est fermé;
  changer ceci n'est plus ouvert.

terminer fermer ceci:
  dire "Ok. C'est fermé.".

-- >>############################################################<<
-- >># PARLER ET INTERROGER                                     #<<
-- >>############################################################<<

-- ==========
--   PARLER
-- ==========

-- a) parler
le joueur peut parler:
  dire "Je pourrais parler tout seul, c’est vrai.".
-- b) parler à ceci
parler à ceci est une action qui concerne un objet visible.

refuser parler à ceci:
  si ceci n'est pas une personne, dire "[intitulé ceci] ne parle pas.".

terminer parler à ceci:
  si ceci réagit, exécuter réaction de ceci;
  sinon dire "(Et ils eurent une conversation peu intéressante.)".
  
-- c) parler à ceci de cela
parler à ceci de cela est une action qui concerne un objet visible et un intitulé.

refuser parler à ceci de cela:
  si ceci n'est pas une personne, dire "[intitulé ceci] ne parle pas.".

exécuter parler à ceci de cela:
  si cela réagit, exécuter réaction de ceci concernant cela.
  
terminer parler à ceci de cela:
  si cela ne réagit pas, dire "(Aucune réponse satisfaisante.)".

-- ==============
--   INTERROGER
-- ==============

-- a) interroger
le joueur peut interroger:
  dire "Je veux bien interroger quelqu’un mais il faut me dire qui et à quel sujet.".

-- b) interroger ceci
interroger ceci est une action qui concerne un objet visible.

refuser interroger à ceci:
  si ceci n'est pas une personne, dire "[intitulé ceci] ne parle pas.".
  
exécuter interroger à ceci:
  dire "Je veux bien l’interroger mais à quel sujet ?".
  
-- c) interroger ceci sur cela
interroger ceci sur cela est une action qui concerne un objet visible et un objet.

refuser interroger ceci sur cela:
  si ceci n'est pas une personne, dire "[intitulé ceci] ne parle pas.".

exécuter interroger ceci sur cela:
  si cela réagit, exécuter réaction de ceci concernant cela.

terminer interroger ceci sur cela:
  si cela ne réagit pas, dire "(Aucune réponse satisfaisante.)".
  
-- >>############################################################<<
-- >># REGARDER ET EXAMINER                                     #<<
-- >>############################################################<<
  
-- ============
--   REGARDER
-- ============

-- a) regarder
regarder est une action.

terminer regarder:
  dire "{_{*[titre ici]*}_}";
  dire "{n}[description ici][contenu ici]";
  dire "{n}{n}[sorties ici]".
  
-- b) regarder ceci
regarder ceci est une action qui concerne un objet visible.

terminer regarder ceci:
   dire "(Pour examiner quelque chose, merci d’utiliser la commande {-examiner-}.)".

-- ============
--   EXAMINER
-- ============

-- a) examiner
le joueur peut examiner:
  dire "(Si vous souhaitez examiner quelque chose, il faut préciser quoi.)".

-- b) examiner ceci
examiner ceci est une action qui concerne un objet visible.

refuser examiner ceci:
  si ceci est une personne, dire "Pas sûr qu'[pronom ceci] ait envie de jouer au docteur.".

terminer examiner ceci:
  dire "[description ceci] ";
  si ceci est une porte ou un contenant :
    dire "{n}[statut ceci]";
  si ceci est un contenant et ouvert:
    dire "[contenu ceci]";
  si ceci est un contenant et fermé et transparent:
    dire "[contenu ceci]";
  si ceci est un support:
    dire "[contenu ceci]";
  si ceci est inaccesssible:
    dire "[pronom ceci] est inaccessible"
  si ceci n'est ni un contenant ni un support:
    dire "[si aucune description pour ceci]Vous ne trouvez rien d’intéressant.[fin si]".

interpréter fouiller comme examiner.

-- >>############################################################<<
-- >># PRENDRE, DONNER, JETER ET DÉPOSER                        #<<
-- >>############################################################<<

-- ===========
--   PRENDRE
-- ===========

-- a) prendre
prendre est une action.

exécuter prendre:
  dire "prendre quoi ?".
  
-- b) prendre ceci
prendre ceci est une action qui concerne un objet visible.

refuser prendre ceci:
  si ceci est possédé, dire "Vous l’avez déjà.";
  si ceci est fixé, dire "C'est fixé.";
  si ceci est inaccessible, dire "Je ne sais pas y accéder.";
  si ceci est une personne ou un animal, dire "Ça ne me parait pas très prudent.";
  si ceci est un décor ou décoratif, dire "Je préfère ne pas m’encombrer avec ça.".

exécuter prendre ceci:
  déplacer ceci vers joueur.
  
terminer prendre ceci:
  dire "[intitulé ceci] a été ajouté[accord ceci] à votre inventaire.".

-- ==========
--   DONNER
-- ==========

-- a) donner
le joueur peut donner:
  dire "Donner quoi ?".
 
-- b) donner ceci
-- donner ceci est une action qui concerne un objet visible.
le joueur peut donner le doublon:
  dire "Donner le doublon à qui du coup ?".
  
-- c) donner ceci à cela
donner ceci à cela est une action qui concerne un objet visible et un vivant visible.

refuser donner ceci à cela:
  si ceci n'est pas possédé, dire "Vous ne possédez pas [intitulé ceci]";
  si cela n'est pas une personne, dire "Je préfère donner ça à quelqu'un.".

exécuter donner ceci à cela:
  déplacer ceci vers cela.

terminer donner ceci à cela:
  dire "Vous avez donné [intitulé ceci] à [intitulé cela].".

-- =========
--   JETER
-- =========

-- a) jeter
le joueur peut jeter:
  dire "jeter quoi ?".
  
-- b) jeter ceci
jeter ceci est une action qui concerne un objet visible.

refuser jeter ceci:
  si ceci n'est pas possédé, dire "Vous ne possédez pas [intitulé ceci]".
  
exécuter jeter ceci:
  déplacer ceci vers ici.
  
terminer jeter ceci:
  dire "Vous avez jeté [intitulé ceci]".

-- ===========
--   METTRE
-- ===========

-- a) mettre
le joueur peut mettre:
  dire "Je sais poser un objet sur un support ou le mettre dans un contenant.".
  
-- c) metre ceci dans cela
mettre ceci dans cela est une action qui concerne un objet visible et un objet visible.

refuser mettre ceci dans cela:
  si ceci n'est pas accessible, dire "[intitulé ceci] n’est pas accessible.";
  si cela n'est pas accessible, dire "[intitulé cela] n’est pas accessible.";
  si cela n'est ni un support ni un contenant, dire "[intitulé ceci] n’est ni un contenant ni un support".

exécuter mettre ceci dans cela:
  déplacer ceci sur cela.

terminer mettre ceci sur cela:
  dire "C’est fait.".

-- >>############################################################<<
-- >># PARTIE 3 : DESCRIPTION DU DONJON                         #<<
-- >>############################################################<<

La salle est un lieu. "Vous êtes dans une salle aux murs gris".
Le seau est un contenant dans la salle.
La table est un support dans la salle.
La bille est un objet dans la salle.