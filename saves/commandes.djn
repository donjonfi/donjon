"Commandes de base"
-- =========================================================
-- PARTIE 1 : COMMANDES DE BASE
-- =========================================================

-- action: REGARDER

-- a) regarder
le joueur peut regarder:
  dire "[description ici]".
  
-- b) regarder ceci
regarder ceci est une action qui concerne un objet visible.

exécuter regarder ceci:
  dire "[description ceci]".

-- action: EXAMINER

-- a) examiner
le joueur peut examiner:
  dire "Que dois-je examiner ?".

-- b) examiner ceci
examiner ceci est une action qui concerne un objet visible.

exécuter examiner ceci:
  dire "[examen ceci] ";
  si ceci est une porte :
    dire "[statut ceci]";
  sinon:
    dire "Vous trouvez: ",
    dire "[contenu ceci]".

-- action: JETER

-- a) jeter
le joueur peut jeter:
  dire "jeter quoi ?".
  
-- b) jeter ceci
jeter ceci est une action qui concerne un objet visible.

refuser jeter ceci:
  si ceci n'est pas possédé, dire "Vous ne possédez pas [intitulé ceci]".
  
exécuter jeter ceci:
  déplacer ceci vers ici.
  
finaliser jeter ceci:
  dire "Vous avez jeté [intitulé ceci]".

-- action: PRENDRE
-- a) prendre
prendre est une action.

exécuter prendre:
  dire "prendre quoi ?".
  
-- b) prendre ceci
prendre ceci est une action qui concerne un objet visible.

refuser prendre ceci:
  si ceci est possédé, dire "Vous l’avez déjà.";
  si ceci est fixé, dire "C'est fixé.";
  si ceci est une personne, dire "Dites-donc !";
  si ceci est un animal, dire "Ça ne me parait pas très prudent.";
  si ceci est un décor, dire "Je préfère ne pas m’encombrer avec ça.".

exécuter prendre ceci:
  déplacer ceci vers joueur.
  
finaliser prendre ceci:
  dire "[intitulé ceci] a été ajouté[accord ceci] à votre inventaire.".

-- MANGER
-- a) manger
le joueur peut manger:
  dire "Manger quoi ?".
-- b) manger ceci
manger ceci est une action qui concerne un objet visible.

refuser manger ceci:
  si ceci est le troll, dire "Grouph! Je pense que c’est plutôt lui qui a faim…";
  si ceci est un animal, dire "Je ne mange pas les animaux crus.";
  si ceci est une personne, dire "Drole d’idée.";
  si ceci n’est pas un légume, dire "Je ne mange que des légumes.".
  
exécuter manger ceci:
  effacer ceci.
  
finaliser manger ceci:
  dire "Vous avez mangé [intitulé ceci].".

-- =========================================================
-- PARTIE 2 : De quoi TESTER les commandes
-- =========================================================

le bureau est un lieu. "Vous êtes dans une salle vide à l’exception d’une chaise, d’un troll est d’un veil homme.".
La chaise est un décor dans le bureau. "[1ère fois]Il s’agit d’une chaise en bois verni.[puis]Elle n’a pas changé de couleur.[fin choix]".

il y a un troll dans le bureau. C’est un animal.
la vache est dans le bureau. C’est un animal.
Le veil homme est une personne dans le bureau.

il y a une tomate dans le bureau. C’est un légume. "Elle a l'air hyper bonne!".
il y a un abricot dans le bureau. C’est un légume. "Il n'a pas l'air mangeable.".