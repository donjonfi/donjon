PARTIE "Informations sur le jeu".

Le titre du jeu est "Coincé".
L’auteur du jeu est "Jonathan Claes".
La version du jeu est "0.8".
Le titre de la licence est "Creative Commons Attribution 4.0 International License".
Le lien de la licence est "https://creativecommons.org/licenses/by/4.0/".

PARTIE "Description du monde".
-- @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
--               MONDE
-- @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

chapitre "Le salon".
-- /////////////////////
--   LIEU 1 − LE SALON
-- /////////////////////
Le salon est un lieu. "Vous êtes dans un petit salon dépouillé.{n}La lumière du jour provient d’une lucarne placée à plusieurs mètres de hauteur.[si la porte secrète est ouverte]{n}Une porte secrète est ouverte au nord du salon.[fin si]".

-- ==========
--   OBJETS
-- ==========
-- > LUCARNE
La lucarne est un objet décoratif dans le salon. "Á cette hauteur elle va être difficile à atteindre.". Elle est inaccessible.
-- > FAUTEUIL
Le fauteuil est un support dans le salon.
Sa description est "C’est un vieux fauteuil en cuir brun bien patiné.".
Son aperçu est "Il y a un fauteuil de style anglais[initialement] au centre de la pièce[puis] que vous avez déplacé[fin choix].".
Interpréter canapé et divan comme fauteuil.
-- > BILLE
Il y a une bille dans le salon. "C'est une bille en verre décorée de petits pois bleus.".
-- > PAPIER
Le bout de papier est un objet caché sur le fauteuil. "Il y a quelque chose d'écrit dessus".
Son aperçu est "[initialement]Il y a un bout de papier froissé coincé entre le coussin et l'accoudoir.[puis]Il y a un bout de papier.[fin choix]".
Interpréter papier et feuille comme le bout de papier.
-- > COFFRE
Le coffre est un contenant fixé dans le salon. "Il s'agit d'un petit coffre surmonté d'un clavier.[si le coffre est verrouillé] Le clavier comporte les lettres de l'alphabet. (Probablement pour pouvoir {-taper-} un code.)[fin si]".
Il est invisible, fermé, ouvrable et verrouillé.
Interpréter le clavier comme le coffre.
-- > ANNEAU
L'anneau est un objet dans le coffre. "C'est un anneau doré. Il est probablement magique."
-- > PORTE
La porte secrète est une porte au nord du salon.
Elle est fermée, verrouillée et invisible.
-- > MUR
Le mur est un support décoratif dans le salon. "Les murs de la pièce sont peints en vert."
-- > TABLEAU
Il y a un tableau sur le mur. "Il s'agit d'une peinture a l'huile. Elle représente un voilier en pleine mer.".
Son aperçu est "Il y a un tableau [si le tableau se trouve sur le mur]accroché à l’un des murs.[sinon]par terre.[fin si]".
Interpréter peinture et cadre comme le tableau.

-- > BOUTON
Le bouton est un objet fixé sur le mur. Il est couvert. "C’est un gros bouton rouge.{n}Juste au-dessus, il est écrit << NE PAS POUSSER SUR LE BOUTON S.V.P. >>".

-- ===========
--   ACTIONS
-- ===========

-- > LIRE PAPIER
Le joueur peut lire le bout de papier:
  dire "Il est écrit << ne pas oublier: {+uaenna+} >>".
-- > POUSSER FAUTEUIL
Le joueur peut pousser le fauteuil:
  si le fauteuil n'est pas déplacé:
    dire "Vous poussez difficilement le fauteuil. Vous découvrez un coffre qui était dissimulé sous le fauteuil.",
    changer le coffre n’est plus invisible,
    changer le fauteuil est déplacé;
  sinon
    dire "Ce n'est plus nécessaire.".
    
interpréter appuyer et déplacer comme pousser.

-- > LANCER BILLE SUR LUCARNE
Lancer ceci vers cela est une action qui concerne la bille et la lucarne.

Exécuter lancer ceci vers cela:
  dire "[au hasard]Vous atteignez la lucarne avec la bille, ça fait << poc >> puis la bille retombe.[ou]Raté.[ou]Bien essayé.[fin choix]".

-- > POUSSER FAUTEUIL
Le joueur peut soulever le fauteuil:
  dire "Il est trop lourd. Par contre vous devriez pouvoir le pousser.".

-- > TAPER CODE
taper ceci est une action qui concerne un intitulé.

refuser taper ceci:
  si le coffre n’est pas présent, dire "Il n'y a pas de clavier ici.";
  si le coffre n'est pas visible, dire "Je ne vois pas de clavier.".
  
exécuter taper ceci:
  si ceci vaut "anneau":
    dire "Le coffre émet un déclic.",
    changer le coffre est déverrouillé;
  sinon
    dire "Rien ne se passe. Ce n'est pas le bon code.".

-- > DÉCROCHER TABLEAU
Le joueur peut décrocher le tableau:
  si le tableau se trouve sur le mur:
    dire "Vous décrochez le tableau et vous découvrez un bouton gros bouton rouge.{n}",
    changer le bouton n’est plus couvert;
  sinon:
    dire "Le taleau n'est pas accroché.".
    
-- ==========
--   RÈGLES
-- ==========

-- > AVANT − PRENDRE TABLEAU
avant prendre le tableau:
  si le tableau se trouve sur le mur:
    dire "Vous décrochez le tableau et vous découvrez un bouton rond.{n}",
    changer le bouton n’est plus couvert,
    changer le tableau se trouve dans le salon,
    stopper l’action.

Le joueur peut pousser le bouton:
  dire "Vous entendez un déclic.";
  si la porte secrète est fermée:
    dire "{n}Une porte s'ouvre dans le mur nord de la pièce.",
    changer la porte secrète est ouverte,
    changer la porte secrète n'est plus invisible.

Chapitre "Le cabinet".
-- ///////////////////////
--   LIEU 2 − LE CABINET
-- ///////////////////////

Le cabinet est un lieu au nord du salon. "Vous vous trouvez dans un petit cabinet rempli de toiles d'araignées."

Les toiles d'araignées sont des décors dans le cabinet. "Brrr c'est n'est pas très rassurant.".
Elles sont décoratives.

La magicienne est une femme dans le cabinet. "Une magicienne vous observe.".

Sa réaction est "<< Sans mon anneau, pas moyen de nous sortir d'ici. >>".
Sa réaction concernant l'anneau est "<< [en boucle]Mon anneau magique m'a été dérobé.[puis]C’est un anneau en or il amplifie ma magie.[puis]Vous finirez bien par le retrouver.[fin choix] >>".
Sa réaction concernant la sortie est "<< Grâce à mon anneau, j'aurais nous faire sortir d’ici >>".
Sa réaction concernant uaenna est "<< Oh ! Vous savez parlez en verlan ! >>".
Sa réaction concernant un sujet inconnu est "<< L’important c’est que vous retrouviez mon anneau afin que je puisse nous sortir d’ici. >>".

après donner pièce à la magicienne:
  dire "<< Elle ne semble pas magique malheureusement. >>".
  
après donner anneau à la magicienne:
  dire "<< Mon héro! >> ";
  dire "{n}{n}La magicienne tend la main et un éclair vous aveugle.";
  dire "{n}{n}Vous êtes de retour chez vous!";
  dire "{n}{n}{+Bravo! Vous avez gagné !+}";
  terminer le jeu.
  
-- 3) objets non placés
La pièce est un objet. "Il s'agit d'une pièce en cuivre.".

PARTIE "Règles".
-- @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
--              RÈGLES
-- @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

avant commencer le jeu :
  changer le joueur se trouve dans le salon;
  changer le joueur possède la pièce;
  changer la porte est ouverte;
  changer la porte n'est plus invisible;
  changer le joueur possède l'anneau.
  
  
PARTIE "Actions".
-- @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
--              ACTIONS
-- @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
chapitre "ouvrir".
-- ==========
--   OUVRIR
-- ==========

-- a) ouvrir ceci
ouvrir ceci est une action qui concerne un objet visible.

refuser ouvrir ceci:
  si ceci n'est pas ouvrable, dire "Ça ne s'ouvre pas.";
  si ceci est verrouillé, dire "C'est verrouillé.".
  
exécuter ouvrir ceci:
  changer ceci est ouvert;
  changer ceci n'est plus fermé.

terminer ouvrir ceci:
  dire "C'est ouvert.";
  si ceci est un contenant, dire "[contenu ceci]".
  
chapitre "fermer".
-- ==========
--   FERMER
-- ==========

-- a) fermer ceci
fermer ceci est une action qui concerne un objet visible.

refuser fermer ceci:
  si ceci n'est pas ouvrable, dire "Ça ne se ferme pas.";
  si ceci est verrouillé, dire "C'est verrouillé.".
  
exécuter fermer ceci:
  changer ceci est fermé;
  changer ceci n'est plus ouvert.

terminer fermer ceci:
  dire "Ok. C'est fermé.".

chapitre "parler".
-- ==========
--   PARLER
-- ==========

-- a) parler
le joueur peut parler:
  dire "Je pourrais parler tout seul, c’est vrai.".
-- b) parler à ceci
parler à ceci est une action qui concerne un objet visible.

refuser parler à ceci:
  si ceci n'est pas une personne, dire "[intitulé ceci] ne parle pas.".

terminer parler à ceci:
  si ceci réagit, exécuter réaction de ceci;
  sinon dire "(Et ils eurent une conversation peu intéressante.)".
  
-- c) parler à ceci de cela
parler à ceci de cela est une action qui concerne un objet visible et un intitulé.

refuser parler à ceci de cela:
  si ceci n'est pas une personne, dire "[intitulé ceci] ne parle pas.".

exécuter parler à ceci de cela:
  si cela réagit, exécuter réaction de ceci concernant cela.
  
terminer parler à ceci de cela:
  si cela ne réagit pas, dire "(Aucune réponse satisfaisante.)".

chapitre "interroger".
-- ==============
--   INTERROGER
-- ==============

-- a) interroger
le joueur peut interroger:
  dire "Je veux bien interroger quelqu’un mais il faut me dire qui et à quel sujet.".

-- b) interroger ceci
interroger ceci est une action qui concerne un objet visible.

refuser interroger à ceci:
  si ceci n'est pas une personne, dire "[intitulé ceci] ne parle pas.".
  
exécuter interroger à ceci:
  dire "Je veux bien l’interroger mais à quel sujet ?".
  
-- c) interroger ceci sur cela
interroger ceci sur cela est une action qui concerne un objet visible et un objet.

refuser interroger ceci sur cela:
  si ceci n'est pas une personne, dire "[intitulé ceci] ne parle pas.".

exécuter interroger ceci sur cela:
  si cela réagit, exécuter réaction de ceci concernant cela.

terminer interroger ceci sur cela:
  si cela ne réagit pas, dire "(Aucune réponse satisfaisante.)".

-- ============
--   REGARDER
-- ============

-- a) regarder
regarder est une action.

terminer regarder:
  dire "{_{*[titre ici]*}_}";
  dire "{n}[description ici][contenu ici]";
  dire "{n}{n}[sorties ici]".
  
-- b) regarder ceci
regarder ceci est une action qui concerne un objet visible.

terminer regarder ceci:
   dire "(Pour examiner quelque chose, merci d’utiliser la commande {-examiner-}.)".

-- ============
--   EXAMINER
-- ============

-- a) examiner
le joueur peut examiner:
  dire "(Si vous souhaitez examiner quelque chose, il faut préciser quoi.)".

-- b) examiner ceci
examiner ceci est une action qui concerne un objet visible.

refuser examiner ceci:
  si ceci est une personne, dire "Pas sûr qu'[pronom ceci] ait envie de jouer au docteur.".

terminer examiner ceci:
  dire "[description ceci] ";
  si ceci est une porte ou un contenant :
    dire "{n}[statut ceci]";
  si ceci est un contenant et ouvert:
    dire "[contenu ceci]";
  si ceci est un contenant et fermé et transparent:
    dire "[contenu ceci]";
  si ceci est un support:
    dire "[contenu ceci]";
  si ceci est inaccesssible:
    dire "[pronom ceci] est inaccessible"
  si ceci n'est ni un contenant ni un support:
    dire "[si aucune description pour ceci]Vous ne trouvez rien d’intéressant.[fin si]".

interpréter fouiller comme examiner.

-- =========
--   JETER
-- =========

-- a) jeter
le joueur peut jeter:
  dire "jeter quoi ?".
  
-- b) jeter ceci
jeter ceci est une action qui concerne un objet visible.

refuser jeter ceci:
  si ceci n'est pas possédé, dire "Vous ne possédez pas [intitulé ceci]".
  
exécuter jeter ceci:
  déplacer ceci vers ici.
  
terminer jeter ceci:
  dire "Vous avez jeté [intitulé ceci]".

-- ===========
--   PRENDRE
-- ===========

-- a) prendre
prendre est une action.

exécuter prendre:
  dire "prendre quoi ?".
  
-- b) prendre ceci
prendre ceci est une action qui concerne un objet visible.

refuser prendre ceci:
  si ceci est possédé, dire "Vous l’avez déjà.";
  si ceci est fixé, dire "C'est fixé.";
  si ceci est inaccessible, dire "Je ne sais pas y accéder.";
  si ceci est une personne ou un animal, dire "Ça ne me parait pas très prudent.";
  si ceci est un décor ou décoratif, dire "Je préfère ne pas m’encombrer avec ça.".

exécuter prendre ceci:
  déplacer ceci vers joueur.
  
terminer prendre ceci:
  dire "[intitulé ceci] a été ajouté[accord ceci] à votre inventaire.".

-- ==========
--   DONNER
-- ==========

-- a) donner
le joueur peut donner:
  dire "Donner quoi ?".
 
-- b) donner ceci
-- donner ceci est une action qui concerne un objet visible.
le joueur peut donner le doublon:
  dire "Donner le doublon à qui du coup ?".
  
-- c) donner ceci à cela
donner ceci à cela est une action qui concerne un objet visible et un vivant visible.

refuser donner ceci à cela:
  si ceci n'est pas possédé, dire "Vous ne possédez pas [intitulé ceci]";
  si cela n'est pas une personne, dire "Je préfère donner ça à quelqu'un.".

exécuter donner ceci à cela:
  déplacer ceci vers cela.

terminer donner ceci à cela:
  dire "Vous avez donné [intitulé ceci] à [intitulé cela].".