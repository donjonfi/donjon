"Le château de la terreur".

-- action: PARLER
-- a) parler
le joueur peut parler:
  dire "Je pourrais parler tout seul, c’est vrai.".
-- b) parler à ceci
parler à ceci est une action qui concerne un objet visible.

refuser parler à ceci:
  si ceci n'est pas une personne, dire "[intitulé ceci] ne parle pas.".

terminer parler à ceci:
  si ceci réagit, exécuter réaction de ceci;
  sinon dire "(Et ils eurent une conversation peu intéressante.)".
  
-- c) parler à ceci de cela
parler à ceci de cela est une action qui concerne un objet visible et un objet.

refuser parler à ceci de cela:
  si ceci n'est pas une personne, dire "[intitulé ceci] ne parle pas.".

exécuter parler à ceci de cela:
  si cela réagit, exécuter réaction de ceci concernant cela.
  
terminer parler à ceci de cela:
  si cela ne réagit pas, dire "(Aucune réponse satisfaisante.)".

-- action: INTERROGER

-- a) interroger
le joueur peut interroger:
  dire "Je veux bien interroger quelqu’un mais il faut me dire qui et à quel sujet.".

-- b) interroger ceci
interroger ceci est une action qui concerne un objet visible.

refuser interroger à ceci:
  si ceci n'est pas une personne, dire "[intitulé ceci] ne parle pas.".
  
exécuter interroger à ceci:
  dire "Je veux bien l’interroger mais à quel sujet ?".
  
-- c) interroger ceci sur cela
interroger ceci sur cela est une action qui concerne un objet visible et un objet.

refuser interroger ceci sur cela:
  si ceci n'est pas une personne, dire "[intitulé ceci] ne parle pas.".

exécuter interroger ceci sur cela:
  si cela réagit, exécuter réaction de ceci concernant cela.

terminer interroger ceci sur cela:
  si cela ne réagit pas, dire "(Aucune réponse satisfaisante.)".

-- action: REGARDER

-- a) regarder
le joueur peut regarder:
  dire "[description ici]".
  
-- b) regarder ceci
regarder ceci est une action qui concerne un objet visible.

exécuter regarder ceci:
   dire "[description ceci]";
   si aucune description pour ceci, dire "C'est [intitulé ceci].".

-- action: EXAMINER

-- a) examiner
le joueur peut examiner:
  dire "Que dois-je examiner ?".

-- b) examiner ceci
examiner ceci est une action qui concerne un objet visible.

refuser examiner ceci:
  si ceci est une personne, dire "Pas sûr qu'il ait envie de jouer au docteur.".

exécuter examiner ceci:
  si ceci est une porte :
    dire "[statut ceci]";
  si ceci contient un objet :
      dire "[examen ceci] ",
      dire "Vous trouvez: ",
      dire "[contenu ceci]",
      changer le joueur possède le contenu de ceci;
  sinon :
      dire "[examen ceci][si aucun examen pour ceci]Vous ne trouvez rien d’intéressant.[fin si]".

-- action: JETER

-- a) jeter
le joueur peut jeter:
  dire "jeter quoi ?".
  
-- b) jeter ceci
jeter ceci est une action qui concerne un objet visible.

refuser jeter ceci:
  si ceci n'est pas possédé, dire "Vous ne possédez pas [intitulé ceci]".
  
exécuter jeter ceci:
  déplacer ceci vers ici.
  
terminer jeter ceci:
  dire "Vous avez jeté [intitulé ceci]".

-- action: PRENDRE
-- a) prendre
prendre est une action.

exécuter prendre:
  dire "prendre quoi ?".
  
-- b) prendre ceci
prendre ceci est une action qui concerne un objet visible.

refuser prendre ceci:
  si ceci est possédé, dire "Vous l’avez déjà.";
  si ceci est fixé, dire "C'est fixé.";
  si ceci est une personne, dire "Dites-donc !";
  si ceci est un animal, dire "Ça ne me parait pas très prudent.";
  si ceci est un décor, dire "Je préfère ne pas m’encombrer avec ça.".

exécuter prendre ceci:
  déplacer ceci vers joueur.
  
terminer prendre ceci:
  dire "[intitulé ceci] a été ajouté[accord ceci] à votre inventaire.".

-- action: DONNER

-- a) donner
le joueur peut donner:
  dire "Donner quoi ?".
 
-- b) donner ceci
-- donner ceci est une action qui concerne un objet visible.
le joueur peut donner le doublon:
  dire "Donner le doublon à qui du coup ?".
  
-- c) donner ceci à cela
donner ceci à cela est une action qui concerne un objet visible et un vivant visible.

refuser donner ceci à cela:
  si ceci n'est pas possédé, dire "Vous ne possédez pas [intitulé ceci]";
  si cela n'est pas une personne, dire "Je préfère donner ça à quelqu'un.".

exécuter donner ceci à cela:
  déplacer ceci vers cela.

terminer donner ceci à cela:
  dire "Vous avez donné [intitulé ceci] à [intitulé cela].".

-- MANGER
-- a) manger
le joueur peut manger:
  dire "Manger quoi ?".
-- b) manger ceci
manger ceci est une action qui concerne un objet visible.

refuser manger ceci:
  si ceci est le troll, dire "Grouph! Je pense que c’est plutôt lui qui a faim…";
  si ceci est un animal, dire "Je ne mange pas les animaux crus.";
  si ceci est une personne, dire "Drole d’idée.";
  si ceci n’est pas un légume, dire "Je ne mange que des légumes.".
  
exécuter manger ceci:
  effacer ceci.
  
terminer manger ceci:
  dire "Vous avez mangé [intitulé ceci].".

-- Partie 1 - Devant le château.

avant commencer le jeu:
	dire "Bienvenue dans {+Le château de la terreur+} !{n}";
	dire "Ton objectif est de repartir avec le trésor.{n}";
	dire "Si c’est ta première partie, utilise la commande « aide » pour en savoir plus sur les commandes disponibles.";
	changer le joueur se trouve dans le cellier;
  changer le joueur possède la clé rouillée.

sauter est une action.
ouvrir ceci avec cela est une action qui concerne un objet possédé et une porte.
combiner ceci avec cela est une action qui concerne deux objets possédés.

-- Chapitre 1 - Devant le portique.
Le départ est un lieu.
Son intitulé est "Devant le portique".
Sa description est "Vous êtes devant le portique du domaine du comte Von Hohenberg. [si le portique est ouvert] Il est ouvert.[sinon] Il est fermé.[fin si] [1ère fois]La nuit va tomber.[2e fois]La nuit vient de tomber.[3e fois]Il fait nuit.[fin choix] Vous entendez encore des grillons.
Vous distinguez une faible lueur à travers les bareaux.".
Il y a des grillons dans le départ. Ce sont des animaux.
"Vous sentez plus que vous ne voyez des petites formes qui se déplacent dans les herbes hautes (il fait noir.)".
-- Chapitre 1b - Le portique.
Le portique est une porte au sud du départ.
"Il s’agit d’un portique en métal. Il est sale et rouillé mais il semble toujours bien solide.".
Il est fermé, verrouillé, rouillé et froid.
La clé rouillée est une clé. "Il s'agit d'une grosse clée rouillée. Probablement celle du portique."
Elle permet de déverrouiller le portique.

-- Chapitre 2 - L'allée.
L'allée (f) est un lieu au sud du départ.
"Vous êtes dans l'allée qui mène à l'entrée du château.
Il y a des torches tout le long du chemin.
Elles vous permettent d'y voir un peu."
Il y a des torches (f) dans l'allée. Ce sont des objets.
-- (pas encore géré).
Si le joueur possède une torche, maintenant le lieu est éclairé.

-- Chapitre 3 - Le cimetière.
Le cimetière est un lieu obscur à l'est de l'allée.
"Vous êtes dans un cimetière lugubre. Il y a un vingtaine de tombes mal entretenues. L'une d'elles est entre-ouverte. Mieux vaut ne pas trop vous en approcher. Vous entendez des bruits vraiment étranges qui ne vous inspirent pas confiance. Il y a aussi un caveau qui s'enfonce dans le sol. De la végétation s'est développée un peu partout.".
-- (pas encore géré).
Si le cimetière est obscur:
  changer sa description est "Vous êtes dans… hum… difficile à dire avec cette obscurité.";
  changer son intitulé est "?".
Il y a des tombes (f) dans le cimetière. Ce sont des décors.
"Je préfère ne pas m'en approcher."
-- Chapitre 3b - Le caveau.
Le caveau est une salle obscure à l'intérieur du cimetière.
"Vous entendez de petits bruissements d'ailes. Probablement des chauves-souris. Quelqu'un est passé avant vous: certains cercueils ont été détruits et des os jonchent le sol.".
Il y a des chauves-souris (f, chauve-souris) dans le caveau. Ce sont des animaux.
Il y a des os (os) dans le caveau. Ce sont des objets.
-- (permet de tester les pluriels irréguliers).
Il y a 2 yeux (œil) dans le cimetière. Ce sont des objets.
Sa description est "On dirait bien qu'il s'agit d'un œil humain. Pas très rassurant.".
-- (pas encore géré).
Ils permettent de se transformer en monstre.

-- Chapitre 4 - Le verger.
Le verger est un lieu obscur à l'ouest de l'allée.
"Vous êtes dans un verger. Il y a de nombreux arbres. Il y a de vieux fruits en décomposition au sol.[si le seau se trouve ici] Il y a un objet brillant par terre.[fin si]".
-- (pas encore géré).
Si le verger est obscur:
  changer sa description est "Vous êtes dans… hum… difficile à dire avec cette obscurité.";
  changer son intitulé est "?".
Il y a des arbres dans le verger. Ce sont des décors.
"Ce sont de grands pommiers. Ils ont été plantés là il y a longtemps.[si le seau se trouve ici] Il y a un seau au pied de l'un d'eux.[fin si]".
Il y a des fruits dans le verger. Ce sont des décors.
"Ces vieux fruits en décomposition ne sont vraiment pas très ragoutant.".
-- (pas encore géré).
Le seau est un contenant dans le verger.
"C'est un grand seau rouge qui semble solide.".
La capacité du seau vaut 4.
-- (pas encore géré).
Si le seau est plein,
	remplacer mettre un objet dans le seau par dire "Le seau est déjà rempli".
Le cellier est une salle obscure au nord du verger.
"Les étagères sont vides mis à part quelques restes de nourriture avariée.
[si le coupe-branches se trouve ici]Il y a un coupe-branches sur l'une d'elles.[fin si]".
Il y a un coupe-branches dans le cellier. C'est un objet. "Il s'agit d'un coupe-branche robuste.[si le joueur possède le seau] Il devrait tenir dans le seau.[fin si]"
Les restes de nourriture sont des décors dans le cellier.

-- Partie 2 - Dans le château du comte Von Hohenberg.

L'entrée est une salle au sud de l'allée.
"Vous êtes devant la porte du château du comte Von Hohenberg.".
Son intitulé est "Devant la porte du château".
La porte d'entrée est une porte au sud de l'entrée.
Elle est fermée, ouvrable et verrouillée.
"Il s'agit d'une porte imposante. Elle est cadenassée."
