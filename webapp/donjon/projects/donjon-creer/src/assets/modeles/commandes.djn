Partie "informations".
-- Titre: "Commandes de bases pour Donjon IDE"
-- Version: 2020-12-28-0007
-- Auteur: Jonathan Claes (https://donjon.xax.be)
-- Licence: Ce fichier est offert sous licence
--   "Creative Commons Attribution 4.0 International License".
--   Plus d'infos: https://creativecommons.org/licenses/by/4.0/

Partie "commandes".
CHAPITRE "ouvrir et fermer".
SCÈNE "ouvrir".
-- ==========
--   OUVRIR
-- ==========
-- a) ouvrir ceci
ouvrir ceci est une action qui concerne un objet visible.

refuser ouvrir ceci:
  si ceci est le joueur, dire "Pardon ?";
  si ceci n'est pas ouvrable, dire "[Pronom ceci] [s’ouvrir ipr pas ceci].";
  si ceci est verrouillé, dire "[Pronom ceci] [être ipr ceci] verrouillé[accord ceci].".
  
exécuter ouvrir ceci:
  changer ceci est ouvert;
  changer ceci n’est plus intact.

terminer ouvrir ceci:
  dire "[Pronom ceci] [être ipr ceci] ouvert[accord ceci].";
  si ceci est un contenant, dire "[contenu ceci]".

L'aide pour l'action ouvrir est "{*Ouvrir*}
  Permet d'ouvrir un objet, une porte, une trappe, ...
  {+exemples+} :
  > {-ouvrir {/la porte rouge/}-}
  > {-ouvrir {/le coffre/}-}
  > {-ouvrir {/le coffre/} avec {/la clé rouge/}-}
  {+raccourci+} : {-ou-}".

ouvrir ceci avec cela est une action qui concerne un objet visible et un objet visible.

terminer ouvrir ceci avec cela:
  dire "Ça n'a pas fonctionné.".

SCÈNE "déverrouiller".
-- =================
--   DÉVERROUILLER
-- =================

déverrouiller ceci avec cela est une action qui concerne un objet visible et un objet visible.

terminer déverrouiller ceci avec cela:
  dire "Ça n'a pas fonctionné.".

SCÈNE "fermer".
-- ==========
--   FERMER
-- ==========

-- a) fermer ceci
fermer ceci est une action qui concerne un objet visible.

refuser fermer ceci:
  si ceci est le joueur, dire "Pardon ?";
  si ceci n'est pas ouvrable, dire "[Pronom ceci] [se fermer ipr pas ceci].";
  si ceci est verrouillé, dire "[Pronom ceci] [être ipr ceci] verrouillé[accord ceci].".
  
exécuter fermer ceci:
  changer ceci est fermé;
  changer ceci n’est plus intact.

terminer fermer ceci:
  dire "[Pronom ceci] [être ipr ceci] fermé[accord ceci].".
  
L'aide pour l'action fermer est "{*Fermer*}
  Permet de fermer un objet, une porte, une trappe, ...
  {+exemples+} :
  > {-fermer {/la grille/}-}
  > {-fermer {/la boite/}-}
  {+raccourci+} : {-fe-}".

CHAPITRE "parler, interroger, montrer".

SCÈNE "parler".
-- ====================
--   PARLER (discuter)
-- ====================

-- a) parler
le joueur peut parler:
  dire "Je pourrais parler tout seul, c’est vrai.".
-- b) parler à ceci
parler à ceci est une action qui concerne un objet visible.

refuser parler avec ceci:
  si ceci n'est pas une personne, dire "[Intitulé ceci] ne parle pas.";
  si ceci est le joueur, dire "Vous entamez un monologue.".

exécuter parler avec ceci:
  si ceci réagit, exécuter réaction de ceci;

terminer parler avec ceci:
  si ceci ne réagit pas dire "(Et ils eurent une conversation peu intéressante.)".
  
-- c) parler à ceci de cela
parler avec ceci concernant cela est une action qui concerne un objet visible et un intitulé.

refuser parler avec ceci concernant cela:
  si ceci n'est pas une personne, dire "[Intitulé ceci] ne parle pas.";
  si ceci est le joueur, dire "Je connais déjà la réponse.".

exécuter parler avec ceci concernant cela:
  si ceci réagit, exécuter réaction de ceci concernant cela.
  
terminer parler avec ceci concernant cela:
  si ceci ne réagit pas, dire "(Aucune réponse satisfaisante.)".
  
L'aide pour l'action parler est "{*Parler*}
  Permet de parler avec une personne. Il est possible de préciser un sujet spécifique.
  {+exemples+} :
  > {-parler avec {/la magicienne/}-}
  > {-parler avec {/le pirate/} concernant {/la carte/}-}
  > {-parler de {/mine/} avec {/nain jaune/} -}
  > {-parler du {/temps/} avec {/l'aubergiste/}-}
  Voir également {-demander-}, {-interroger-}, {-montrer-} et {-donner-}.
  {+raccourci+} : {-par-}".

interpréter discuter comme parler.

SCÈNE "interroger".
-- ===========================
--   INTERROGER (questionner)
-- ===========================

-- a) interroger
le joueur peut interroger:
  dire "Je veux bien interroger quelqu’un mais il faut me dire qui et à quel sujet.".

-- b) interroger ceci
interroger ceci est une action qui concerne un objet visible.

refuser interroger ceci:
  si ceci n'est pas une personne, dire "[Intitulé ceci] ne parle pas.";
  si ceci est le joueur, dire "Je connais déjà la réponse.".
  
exécuter interroger ceci:
  si ceci réagit, exécuter réaction de ceci;

terminer interroger ceci:
  si ceci ne réagit pas dire "(Et ils eurent une conversation peu intéressante.)".
  
-- c) interroger ceci concernant cela
interroger ceci concernant cela est une action qui concerne un objet visible et un intitulé.

refuser interroger ceci concernant cela:
  si ceci n'est pas une personne, dire "[Intitulé ceci] ne parle pas.".

exécuter interroger ceci concernant cela:
  si ceci réagit, exécuter réaction de ceci concernant cela.

terminer interroger ceci concernant cela:
  si ceci ne réagit pas, dire "(Aucune réponse satisfaisante.)".
  
L'aide pour l'action interroger est "{*Interroger*}
  Permet d'interroger une personne sur un sujet spécifique.
  {+exemples+} :
  > {-interroger {/l'aubergiste/} concernant {/le menu/}-}
  > {-interroger {/le villageois/} à propos de {/la fête/}-}
  > {-interroger {/roi/} sur {/tournois/}-}
  {+Voir également+} : {-parler-}, {-demander-}, {-montrer-} et {-donner-}.
  {+raccourci+} : {-int-}".
  
interpréter questionner comme interroger.

SCÈNE "demander".
-- ==============
--   DEMANDER
-- ==============

-- a) demander
le joueur peut demander:
  dire "Je sais demander mais il faut me dire quoi et à qui.".

-- b) demander ceci
demander ceci est une action qui concerne un objet visible.
  
exécuter demander ceci:
  dire "Je sais demander mais il faut me dire quoi et à qui.".
  
-- c) demander ceci à cela
demander ceci à cela est une action qui concerne un intitulé et un objet visible.

refuser demander ceci à cela:
  si cela n'est pas une personne, dire "[Intitulé cela] ne parle pas.".

exécuter demander ceci à cela:
  si cela réagit, exécuter réaction de cela concernant ceci.

terminer demander ceci à cela:
  si cela ne réagit pas, dire "(Aucune réponse satisfaisante.)".
  
L'aide pour l'action demander est "{*Demander*}
  Permet de demander quelque chose à une personne.
  {+exemples+} :
  > {-demander {/une chambre/} à {/l'aubergiste/}-}
  > {-demander {/à manger/} au {/cuisinier/}-}
  {+Voir également+} : {-parler-}, {-questionner-}, {-montrer-} et {-donner-}.
  {+raccourci+} : {-dem-}".

SCÈNE "montrer".
-- ==========
--   MONTRER
-- ==========
  
-- c) montrer ceci à cela
montrer ceci à cela est une action qui concerne un objet visible et un vivant.

refuser montrer ceci à cela:
  si cela est le joueur, dire "Je pense qu'[pronom cela] me voit déjà.".

exécuter montrer ceci à cela:
  si cela réagit, exécuter réaction de cela concernant ceci.
  
terminer montrer ceci à cela:
  si cela ne réagit pas, dire "(Aucune réaction satisfaisante.)".
  
L'aide pour l'action montrer est "{*Montrer*}
  Permet de montrer un objet à une personne.
  {+exemples+} :
  > {-montrer {/la baguette/} à {/la fée/}-}
  > {-montrer {/chausette/} au {/lutin/}-}
  {+Voir également+} : {-parler-}, {-questionner-}, {-demander-} et {-donner-}.
  {+raccourci+} : {-mon-}".
  
CHAPITRE "insulter, énerver, crier".

SCÈNE "insulter".
-- ============
--   INSULTER
-- ============
  
-- insulter ceci
insulter ceci est une action qui concerne un objet visible.

refuser insulter ceci:
  si ceci est le joueur, dire "Vous vous insultez copieusement. (Ça va passer…)".
  
terminer insulter ceci:
  dire "Vous insultez copieusement [intitulé ceci].".


SCÈNE "énerver".
-- ============
--   ÉNERVER
-- ============
  
-- énerver ceci 
énerver ceci est une action qui concerne un objet visible.

refuser énerver ceci:
  si ceci est le joueur, dire "Vous vous énervez un bon coup.";
  si ceci n’est pas un vivant, dire "Rien à faire, [pronom ceci] reste impassible.".
  
terminer énerver ceci:
  dire "Vous essayez d’énerver [intitulé ceci] sans succès.".


SCÈNE "crier".
-- ============
--   CRIER
-- ============
  
-- crier 
crier est une action.

terminer énerver ceci:
  dire "Vous criez un bon coup.".

-- crier sur ceci
crier sur ceci est une action qui concerne un objet visible.

refuser crier sur ceci:
  si ceci est le joueur, dire "Vous vous criez dessus. (Ça va passer…)".
  
terminer crier sur ceci:
  dire "Vous criez sur [intitulé ceci]. (Ça ne sert à rien.)".

CHAPITRE "regarder et examiner".
SCÈNE "regarder".
-- ============
--   REGARDER
-- ============

-- a) regarder
regarder est une action.

terminer regarder:
  dire "{_{*[titre ici]*}_}";
  dire "{n}[description ici][contenu ici]";
  dire "{n}{n}[sorties ici]".
  
-- b) regarder ceci
regarder ceci est une action qui concerne un élément visible.

terminer regarder ceci:
  si ceci est un objet:
    dire "[description ceci] ";
  si ceci est une porte ou un contenant :
    dire "{n}[statut ceci]";
  si ceci est un contenant et ouvert:
    dire "[contenu ceci]";
  si ceci est un contenant et fermé et transparent:
    dire "[contenu ceci]";
  si ceci est un support:
    dire "[contenu ceci]";
  si ceci n'est ni accessible ni un lieu:
    dire " [Pronom ceci] n'est pas accessible.";
  si ceci n'est ni un contenant ni un support:
    dire "[si aucune description pour ceci]Vous ne trouvez rien d’intéressant.[fin si]";
  si ceci est un lieu:
    dire "{_{*[titre ici]*}_}",
    dire "{n}[description ici][contenu ici]",
    dire "{n}{n}[sorties ici]".
  
L'aide pour l'action regarder est "{*Regarder*}
  Permet de regarder autours de vous pour en savoir plus sur le lieu où vous vous trouvez.
  {+exemple+} :
  > {-regarder-}
  {+raccourci+} : {-re-}".

SCÈNE "examiner".
-- ============
--   EXAMINER
-- ============

-- a) examiner
le joueur peut examiner:
  dire "(Si vous souhaitez examiner quelque chose, il faut préciser quoi.)".

-- b) examiner ceci
examiner ceci est une action qui concerne un élément visible.

refuser examiner ceci:
  si ceci est une personne, dire "Pas sûr qu'[pronom ceci] ait envie de jouer au docteur.".

terminer examiner ceci:
  si ceci est le joueur:
    dire "Étrange...{n}";
  si ceci est un objet:
    dire "[description ceci] ";
  si ceci est une porte ou un contenant :
    dire "{n}[statut ceci]";
  si ceci est un contenant et ouvert:
    dire "[contenu ceci]";
  si ceci est un contenant et fermé et transparent:
    dire "[contenu ceci]";
  si ceci est un support:
    dire "[contenu ceci]";
  si ceci n'est ni accessible ni un lieu:
    dire " [Pronom ceci] n'est pas accessible.";
  si ceci n'est ni un contenant ni un support:
    dire "[si aucune description pour ceci]Vous ne trouvez rien d’intéressant.[fin si]";
  si ceci est un lieu:
    dire "Veuillez préciser quel élément vous souhaitez examiner.".
    
interpréter fouiller comme examiner.

L'aide pour l'action examiner est "{*Examiner*}
  Permet d'examiner un élément du jeu pour avoir des détails ou trouver un objet.
  {+exemples+} :
  > {-examiner {/l'arbre/}-}
  > {-examiner {/l'épée/}}
  {+raccourci+} : {-ex-}".

CHAPITRE "prendre, donner, jeter et déposer".
SCÈNE "prendre".
-- ===========
--   PRENDRE
-- ===========

-- a) prendre
prendre est une action.

exécuter prendre:
  dire "prendre quoi ?".
  
-- b) prendre ceci
prendre ceci est une action qui concerne un objet visible.

refuser prendre ceci:
  si ceci est possédé, dire "Vous l’avez déjà.";
  si ceci est le joueur, dire "Pardon ?";
  si ceci est une personne ou un animal, dire "Ça ne me parait pas très prudent.";
  si ceci n'est pas accessible, dire "Je ne sais pas y accéder.";
  si ceci est un décor ou décoratif, dire "Je préfère ne pas m’encombrer avec ça.";
  si ceci est fixé, dire "[Pronom ceci] [être ipr ceci] fixé[accord ceci].".

exécuter prendre ceci:
  déplacer ceci vers joueur.
  
terminer prendre ceci:
  dire "[Intitulé ceci] [être pac ceci] ajouté[accord ceci] à votre inventaire.".

L'aide pour l'action prendre est "{*Prendre*}
  Permet de prendre un objet accessible pour le mettre dans votre inventaire.
  {+exemples+} :
  > {-prendre {/l'épée/}-}
  > {-prendre {/le bouquet de fleurs/}-}
  {+raccourci+} : {-p-}".

SCÈNE "donner".
-- ==========
--   DONNER
-- ==========
-- a) donner
le joueur peut donner:
  dire "Donner quoi ?".
  
-- c) donner ceci à cela
donner ceci à cela est une action qui concerne un objet visible et un vivant visible.

refuser donner ceci à cela:
  si ceci n'est pas possédé, dire "Vous ne possédez pas [intitulé ceci]";
  si ceci est le joueur, dire "Ça ne va pas ?";
  si cela n'est pas une personne, dire "Je préfère donner ça à quelqu'un.".

exécuter donner ceci à cela:
  déplacer ceci vers cela.

terminer donner ceci à cela:
  dire "Vous avez donné [intitulé ceci] à [intitulé cela].".
  
L'aide pour l'action donner est "{*Donner*}
  Permet de donner un objet que vous possédez à quelqu'un.
  {+exemples+} :
  > {-donner {/la poudre magique/} à {/la magicienne/}-}
  > {-donner {/la pomme/} au {/nain/}-}
  {+raccourci+} : {-d-}".

SCÈNE "jeter".
-- =========
--   JETER
-- =========

-- a) jeter
le joueur peut jeter:
  dire "jeter quoi ?".
  
-- b) jeter ceci
jeter ceci est une action qui concerne un objet visible.

refuser jeter ceci:
  si ceci n'est pas accessible, dire "[intitulé ceci] n’est pas accessible.";
  si ceci est le joueur, dire "Je préfère ne pas me jeter. Drôle d'idée.";
  si ceci est un vivant, dire "Je suis contre la violence.".
  
exécuter jeter ceci:
  déplacer ceci vers ici.
  
terminer jeter ceci:
  dire "Je [l' ceci]ai jeté[accord ceci].".

L'aide pour l'action jeter est "{*Jeter*}
  Permet de jeter un objet que vous possédez sur le sol.
  {+exemple+} :
  > {-jeter {/la fleur/}-}
  {+raccourci+} : {-j-}".
  
-- b) jeter ceci vers cela
jeter ceci vers cela est une action qui concerne deux objets visibles.

refuser jeter ceci vers cela:
  si ceci n'est pas accessible, dire "[intitulé ceci] n’est pas accessible.";
  si ceci est le joueur, dire "Je préfère ne pas me jeter. Drôle d'idée.";
  si cela est un vivant, dire "Je suis contre la violence.".
  
exécuter jeter ceci vers cela:
  si cela est un support :
    déplacer ceci vers cela;
  sinon
    déplacer ceci vers ici.
  
terminer jeter ceci vers cela:
  dire "Je [l' ceci]ai jeté[accord ceci].".

L'aide pour l'action jeter est "{*Jeter*}
  Permet de jeter un objet que vous possédez sur le sol.
  {+exemple+} :
  > {-jeter {/la fleur/}-}
  {+raccourci+} : {-j-}".

interpréter lancer comme jeter.

SCÈNE "mettre".
-- ===========
--   METTRE
-- ===========

-- b) metre ceci (sur moi)
mettre ceci est une action qui concerne un objet visible.

refuser mettre ceci:
  si ceci n'est pas accessible, dire "[intitulé ceci] n’est pas accessible.";
  si ceci est le joueur, dire "Vous avez de drôles d'idées.";
  si ceci n'est pas portable, dire "Je ne peux pas porter [intitulé ceci] sur moi.".

exécuter mettre ceci:
  changer le joueur porte ceci.
  
terminer mettre ceci:
  dire "Vous portez [intitulé ceci].".

interpréter chausser, enfiler, porter, équiper et tenir comme mettre.
  
-- c) metre ceci dans cela
mettre ceci dans cela est une action qui concerne un objet visible et un objet visible.

refuser mettre ceci dans cela:
  si ceci n'est pas accessible, dire "[intitulé ceci] n’est pas accessible.";
  si cela n'est pas accessible, dire "[intitulé cela] n’est pas accessible.";
  si cela n'est ni un support ni un contenant, dire "[intitulé cela] n’est ni un contenant ni un support.";
  si cela est fermé, dire "[intitulé cela] est fermé[accord cela].".

exécuter mettre ceci dans cela:
  déplacer ceci sur cela.

terminer mettre ceci sur cela:
  dire "C’est fait.".

interpréter poser comme mettre.

L'aide pour l'action mettre est "{*Mettre / Poser / Tenir / Enfiler / Chausser*}
  Permet de poser un objet sur un support ou de le mettre dans un contenant.
  {+exemples+} :
  > {-mettre {/la pomme/} dans {/le panier/}-}
  > {-poser {/l'épée/} sur {/la table/}-}
  > {-tenir {/le bouclier/}-}
  > {-enfiler {/la tunique/}-}
  > {-chausser {/les bottes/}-}
  {+raccourci+} : {-me-}, {-po-}, {-te-}, {-enf-}, {-ch-}".

SCÈNE "enlever".
-- ===========
--   ENLEVER
-- ===========

enlever ceci est une action qui concerne un objet visible.

refuser enlever ceci:
  si ceci est portable mais pas porté, dire "Vous ne portez pas [intitulé ceci].";
  si ceci est le joueur, dire "Vous organisez votre enlèvement... et votre sauvetage.";
  si ceci est fixé, dire "[Pronom ceci] [être ipr ceci] fixé[accord ceci].".

exécuter enlever ceci:
  changer le joueur ne porte plus ceci.

terminer enlever ceci:
  dire "Vous ne portez plus [intitulé ceci].".
  
interpréter retirer comme enlever.


CHAPITRE "déplacer, pousser, tirer".

SCÈNE "déplacer".
-- ===========
--   DÉPLACER
-- ===========
déplacer ceci est une action qui concerne un objet visible.

refuser déplacer ceci:
  si ceci est inaccessible, dire "Je n’y ai pas accès.";
  si ceci est le joueur, dire "La commande {-aller-} devrait vous convenir.";
  si ceci est fixé, dire "[Pronom ceci] [être ipr ceci] fixé[accord ceci].".
  
exécuter déplacer ceci:
  changer ceci est déplacé.
  
terminer déplacer ceci:
  dire "Ça n’a rien donné.".

interpréter bouger comme déplacer.

SCÈNE "pousser".
-- ===========
--   POUSSER
-- ===========
pousser ceci est une action qui concerne un objet visible.

refuser pousser ceci:
  si ceci est inaccessible, dire "Je n’y ai pas accès.";
  si ceci est le joueur, dire "Je suis dans le chemin ?".

exécuter pousser ceci:
  changer ceci est déplacé.
  
terminer pousser ceci:
  dire "Ça n’a rien donné.".

interpréter appuyer et enfoncer comme pousser.

SCÈNE "tirer".
-- ===========
--   TIRER
-- ===========
tirer ceci est une action qui concerne un objet visible.

refuser tirer ceci:
  si ceci est inaccessible, dire "Je n’y ai pas accès.";
  si ceci est le joueur, dire "Pardon ?".

exécuter tirer ceci:
  changer ceci est déplacé.
  
terminer tirer ceci:
  dire "Ça n’a rien donné.".

CHAPITRE "pousser, tirer".

SCÈNE "toucher".
-- ===========
--   TOUCHER
-- ===========
toucher ceci est une action qui concerne un objet visible.

refuser toucher ceci:
  si ceci est inaccessible, dire "Je n’y ai pas accès.";
  si ceci est le joueur, dire "Non vous ne revez pas.";
  si ceci est un vivant, dire "Vous lui avez demandé son avis ?".

exécuter toucher ceci:
  changer ceci n’est plus intact.

terminer toucher ceci:
  dire "Je [l’ ceci]ai touché[accord ceci].".

interpréter pincer comme toucher.

SCÈNE "secouer".
-- ===========
--   SECOUER
-- ===========
secouer ceci est une action qui concerne un objet visible.

refuser secouer ceci:
  si ceci est inaccessible, dire "Je n’y ai pas accès.";
  si ceci est le joueur, dire "Rien de tel pour se réveiller.";
  si ceci est un vivant, dire "Je suis contre la violence.".

exécuter secouer ceci:
  changer ceci n’est plus intact.

terminer secouer ceci:
  dire "Je [l’ ceci]ai secoué[accord ceci].".

SCÈNE "casser".
-- ===========
--   CASSER
-- ===========
casser ceci est une action qui concerne un objet visible.

refuser casser ceci:
  si ceci est inaccessible, dire "Je n’y ai pas accès.";
  si ceci est le joueur, dire "Ça ne va pas ?";
  si ceci est un vivant, dire "Vous avez de drôles d’idées.".

terminer casser ceci:
  dire "Ça peut encore servir.".

interpréter déchirer et détruire comme casser.

SCÈNE "brûler".
-- ===========
--   BRÛLER
-- ===========
brûler ceci est une action qui concerne un objet visible.

refuser brûler ceci:
  si ceci est inaccessible, dire "Je n’y ai pas accès.";
  si ceci est le joueur, dire "Ça ne va pas ?";
  si ceci est un vivant, dire "Vous avez de drôles d’idées.".

terminer brûler ceci:
  dire "Ça peut encore servir.".

interpréter bruler comme brûler.

CHAPITRE "manger, boire".

SCÈNE "manger".
-- ===========
--   MANGER
-- ===========
manger ceci est une action qui concerne un objet visible.

refuser manger ceci:
  si ceci est inaccessible, dire "Je n’y ai pas accès.";
  si ceci est un vivant mais pas mangeable, dire "Vous avez de drôles d’idées.";
  si ceci n’est pas mangeable, dire "Ça ne se mange pas voyons !".
  
exécuter manger ceci:
  changer effacer ceci.

terminer manger ceci:
  dire "Je [l’ ceci]ai mangé[accord ceci]. Maintenant je ne [l’ ceci]ai plus.".

interpréter engloutir comme manger.

SCÈNE "boire".
-- ===========
--   BOIRE
-- ===========
boire ceci est une action qui concerne un objet visible.

refuser boire ceci:
  si ceci est inaccessible, dire "Je n’y ai pas accès.";
  si ceci est un vivant mais pas buvable, dire "Vous avez de drôles d’idées.";
  si ceci n’est pas buvable, dire "Ça ne se boit pas voyons !".
  
exécuter boire ceci:
  changer effacer ceci.

terminer boire ceci:
  dire "Je [l’ ceci]ai bu[accord ceci]. Maintenant je ne [l’ ceci]ai plus.".


CHAPITRE "frapper, tuer, assommer".

SCÈNE "frapper".
-- ===========
--   FRAPPER
-- ===========
frapper ceci est une action qui concerne un objet visible.
  
refuser frapper ceci:
  si ceci est le joueur, dire "Ça ne va pas ?".

terminer frapper ceci:
  dire "Je suis contre la violence.".
  
SCÈNE "tuer".
-- ===========
--    TUER
-- ===========
tuer ceci est une action qui concerne un objet visible.

refuser tuer ceci:
  si ceci n’est pas un vivant, dire "[Pronom ceci] [vivre ipr pas ceci].";
  si ceci est le joueur, dire "Ça ne va pas ?".
  
terminer tuer ceci:
  dire "Je suis contre la violence.".
  
SCÈNE "embrasser".
-- =============
--   EMBRASSER
-- =============
embrasser ceci est une action qui concerne un objet visible.

refuser embrasser ceci:
  si ceci est le joueur, dire "Pardon ?";
  si ceci n’est pas un vivant, dire "Vous avez de drôles d’idées.".
  
terminer embrasser ceci:
  dire "Vous lui avez demandé son avis ?".
  
SCÈNE "enlacer".
-- ============
--   ENLACER
-- ============
enlacer ceci est une action qui concerne un objet visible.

refuser enlacer ceci:
  si ceci est le joueur, dire "Vous vous faites un gros câlin.";
  si ceci n’est pas un vivant, dire "Vous avez de drôles d’idées.".
  
terminer enlacer ceci:
  dire "Vous lui avez demandé son avis ?".
  
SCÈNE "assommer".
-- ===========
--   ASSOMMER
-- ===========
assommer ceci est une action qui concerne un objet visible.

refuser assommer ceci:
  si ceci est le joueur, dire "Il doit y a voir un autre moyen de résoudre votre problème.";
  si ceci n’est pas un vivant, dire "[Pronom ceci] [vivre ipr pas ceci].".

terminer assommer ceci:
  dire "Je suis contre la violence.".

CHAPITRE "écouter, sentir, dormir, prier".

SCÈNE "écouter".
-- ===========
--   ÉCOUTER
-- ===========
écouter est une action.

terminer écouter:
  dire "Vous prenez le temps d’écouter les sons qui vous entourent.".

écouter ceci est une action qui concerne un objet visible.

refuser écouter ceci:
  si ceci est le joueur, dire "Vous prenez le temps de vous écouter. Hein ?".

terminer écouter ceci:
  dire "Vous prenez le temps d’écouter [intitulé ceci].".

SCÈNE "dormir".
-- ===========
--   DORMIR
-- ===========
dormir est une action.

terminer dormir:
  dire "Vous prenez un repo bien mérité, quoi qu’un peu inconfortable.".
  
  
dormir sur ceci est une action qui concerne un objet visible.

refuser dormir sur ceci:
  si ceci est le joueur, dire "Pardon ?";
  si ceci n’est pas un support, dire "Je ne sais pas me coucher dessus.".

terminer dormir sur ceci:
  dire "Vous prenez un repo bien mérité.".
  

SCÈNE "prier".
-- ===========
--   PRIER
-- ===========
prier est une action.

terminer prier:
  dire "Vous priez.".
  
prier ceci est une action qui concerne un intitulé.

refuser prier ceci:
  si ceci est le joueur, dire "Vous vous appréciez beaucoup. C'est bien.".

terminer prier ceci:
  dire "Vous priez [intitulé ceci].".

CHAPITRE "chanter, danser, sauter, attendre".

SCÈNE "chanter".
-- ===========
--   CHANTER
-- ===========
chanter est une action.

terminer chanter:
  dire "Vous entonnez une chanson joyeuse.".

SCÈNE "danser".
-- ===========
--   DANSER
-- ===========
danser est une action.

terminer danser:
  dire "Vous réalisez une petite danse.".
  
SCÈNE "sauter".
-- ===========
--   SAUTER
-- ===========
sauter est une action.

terminer sauter:
  dire "Vous sautez sur place.".
  
SCÈNE "attendre".
-- ============
--   ATTENDRE
-- ============
attendre est une action.

terminer attendre:
  dire "Vous attendez.".

CHAPITRE "lire".

SCÈNE "lire".
-- ===========
--    LIRE
-- ===========
lire ceci est une action qui concerne un objet visible.

refuser lire ceci:
  si ceci est le joueur, dire "Vous lisez en vous. Hein ?";
  si aucun texte pour ceci, dire "Je ne vois rien à lire.".

terminer lire ceci:
  dire "[texte ceci]".

CHAPITRE "péter".

SCÈNE "péter".
-- ===========
--    PÉTER
-- ===========
péter est une action.

terminer péter:
  dire "Vous faites un bruit malpoli.".

interpréter roter comme péter.

