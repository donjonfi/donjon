Partie "informations".
-- Titre: "Commandes de bases pour Donjon IDE"
-- Version: 2020-11-12-0001
-- Auteur: Jonathan Claes (https://donjon.xax.be)
-- Licence: Ce fichier est offert sous licence
--   "Creative Commons Attribution 4.0 International License".
--   Plus d'infos: https://creativecommons.org/licenses/by/4.0/

Partie "commandes".
CHAPITRE "ouvrir et fermer".
SCÈNE "ouvrir".
-- ==========
--   OUVRIR
-- ==========
-- a) ouvrir ceci
ouvrir ceci est une action qui concerne un objet visible.

refuser ouvrir ceci:
  si ceci n'est pas ouvrable, dire "Ça ne s'ouvre pas.";
  si ceci est verrouillé, dire "C'est verrouillé.".
  
exécuter ouvrir ceci:
  changer ceci est ouvert;
  changer ceci n'est plus fermé.

terminer ouvrir ceci:
  dire "C'est ouvert.";
  si ceci est un contenant, dire "[contenu ceci]".

L'aide pour l'action ouvrir est "{*Ouvrir*}
  Permet d'ouvrir un objet, une porte, une trappe, ...
  {+exemples+} :
  > {-ouvrir {/la porte rouge/}-}
  > {-ouvrir {/le coffre/}-}
  {+raccourci+} : {-ou-}".

SCÈNE "fermer".
-- ==========
--   FERMER
-- ==========

-- a) fermer ceci
fermer ceci est une action qui concerne un objet visible.

refuser fermer ceci:
  si ceci n'est pas ouvrable, dire "Ça ne se ferme pas.";
  si ceci est verrouillé, dire "C'est verrouillé.".
  
exécuter fermer ceci:
  changer ceci est fermé;
  changer ceci n'est plus ouvert.

terminer fermer ceci:
  dire "Ok. C'est fermé.".
  
L'aide pour l'action fermer est "{*Fermer*}
  Permet de fermer un objet, une porte, une trappe, ...
  {+exemples+} :
  > {-fermer {/la grille/}-}
  > {-fermer {/la boite/}-}
  {+raccourci+} : {-fe-}".

CHAPITRE "parler et interroger".

SCÈNE "parler".
-- ==========
--   PARLER
-- ==========

-- a) parler
le joueur peut parler:
  dire "Je pourrais parler tout seul, c’est vrai.".
-- b) parler à ceci
parler à ceci est une action qui concerne un objet visible.

refuser parler à ceci:
  si ceci n'est pas une personne, dire "[intitulé ceci] ne parle pas.".

terminer parler à ceci:
  si ceci réagit, exécuter réaction de ceci;
  sinon dire "(Et ils eurent une conversation peu intéressante.)".
  
-- c) parler à ceci de cela
parler à ceci de cela est une action qui concerne un objet visible et un intitulé.

refuser parler à ceci de cela:
  si ceci n'est pas une personne, dire "[intitulé ceci] ne parle pas.".

exécuter parler à ceci de cela:
  si cela réagit, exécuter réaction de ceci concernant cela.
  
terminer parler à ceci de cela:
  si cela ne réagit pas, dire "(Aucune réponse satisfaisante.)".
  
L'aide pour l'action parler est "{*Parler*}
  Permet de parler avec une personne. Il est possible de préciser un sujet spécifique.
  {+exemples+} :
  > {-parler à {/la magicienne/}-}
  > {-parler de {/la carte/} au {/pirate/}-}
  > {-parler au {/nain jaune/} de {/la mine/}-}
  > {-parler de {/manger/} à {/l'aubergiste/}-}
  {+raccourci+} : {-pa-}".

SCÈNE "interroger".
-- ==============
--   INTERROGER
-- ==============

-- a) interroger
le joueur peut interroger:
  dire "Je veux bien interroger quelqu’un mais il faut me dire qui et à quel sujet.".

-- b) interroger ceci
interroger ceci est une action qui concerne un objet visible.

refuser interroger à ceci:
  si ceci n'est pas une personne, dire "[intitulé ceci] ne parle pas.".
  
exécuter interroger à ceci:
  dire "Je veux bien l’interroger mais à quel sujet ?".
  
-- c) interroger ceci sur cela
interroger ceci sur cela est une action qui concerne un objet visible et un objet.

refuser interroger ceci sur cela:
  si ceci n'est pas une personne, dire "[intitulé ceci] ne parle pas.".

exécuter interroger ceci sur cela:
  si cela réagit, exécuter réaction de ceci concernant cela.

terminer interroger ceci sur cela:
  si cela ne réagit pas, dire "(Aucune réponse satisfaisante.)".
  
L'aide pour l'action interroger est "{*Interroger / Demander*}
  Permet d'interroger une personne sur un sujet spécifique.
  {+exemples+} :
  > {-interroger {/l'aubergiste/} concernant {/le menu/}-}
  > {-interroger {/le villageois/} à propos de {/la fête/}-}
  > {-demander {/une épée/} au {/vendeur/}-}
  {+raccourci+} : {-int-}, {-dem-}".
  
CHAPITRE "regarder et examiner".
SCÈNE "regarder".
-- ============
--   REGARDER
-- ============

-- a) regarder
regarder est une action.

terminer regarder:
  dire "{_{*[titre ici]*}_}";
  dire "{n}[description ici][contenu ici]";
  dire "{n}{n}[sorties ici]".
  
-- b) regarder ceci
regarder ceci est une action qui concerne un objet visible.

terminer regarder ceci:
   dire "(Pour examiner quelque chose, merci d’utiliser la commande {-examiner-}.)".

L'aide pour l'action regarder est "{*Regarder*}
  Permet de regarder autours de vous pour en savoir plus sur le lieu où vous vous trouvez.
  {+exemple+} :
  > {-regarder-}
  {+raccourci+} : {-re-}".

SCÈNE "examiner".
-- ============
--   EXAMINER
-- ============

-- a) examiner
le joueur peut examiner:
  dire "(Si vous souhaitez examiner quelque chose, il faut préciser quoi.)".

-- b) examiner ceci
examiner ceci est une action qui concerne un objet visible.

refuser examiner ceci:
  si ceci est une personne, dire "Pas sûr qu'[pronom ceci] ait envie de jouer au docteur.".

terminer examiner ceci:
  dire "[description ceci] ";
  si ceci est une porte ou un contenant :
    dire "{n}[statut ceci]";
  si ceci est un contenant et ouvert:
    dire "[contenu ceci]";
  si ceci est un contenant et fermé et transparent:
    dire "[contenu ceci]";
  si ceci est un support:
    dire "[contenu ceci]";
  si ceci n'est pas accessible:
    dire "[pronom ceci] n'est pas accessible."
  si ceci n'est ni un contenant ni un support:
    dire "[si aucune description pour ceci]Vous ne trouvez rien d’intéressant.[fin si]".

interpréter fouiller comme examiner.

L'aide pour l'action examiner est "{*Examiner*}
  Permet d'examiner un élément du jeu pour avoir des détails ou trouver un objet.
  {+exemples+} :
  > {-examiner {/l'arbre/}-}
  > {-examiner {/l'épée/}}
  {+raccourci+} : {-ex-}".

CHAPITRE "prendre, donner, jeter et déposer".
SCÈNE "prendre".
-- ===========
--   PRENDRE
-- ===========

-- a) prendre
prendre est une action.

exécuter prendre:
  dire "prendre quoi ?".
  
-- b) prendre ceci
prendre ceci est une action qui concerne un objet visible.

refuser prendre ceci:
  si ceci est possédé, dire "Vous l’avez déjà.";
  si ceci est fixé, dire "C'est fixé.";
  si ceci n'est pas accessible, dire "Je ne sais pas y accéder.";
  si ceci est une personne ou un animal, dire "Ça ne me parait pas très prudent.";
  si ceci est un décor ou décoratif, dire "Je préfère ne pas m’encombrer avec ça.".

exécuter prendre ceci:
  déplacer ceci vers joueur.
  
terminer prendre ceci:
  dire "[intitulé ceci] a été ajouté[accord ceci] à votre inventaire.".

L'aide pour l'action prendre est "{*Prendre*}
  Permet de prendre un objet accessible pour le mettre dans votre inventaire.
  {+exemples+} :
  > {-prendre {/l'épée/}-}
  > {-prendre {/le bouquet de fleurs/}-}
  {+raccourci+} : {-p-}".

SCÈNE "donner".
-- ==========
--   DONNER
-- ==========
-- a) donner
le joueur peut donner:
  dire "Donner quoi ?".
  
-- c) donner ceci à cela
donner ceci à cela est une action qui concerne un objet visible et un vivant visible.

refuser donner ceci à cela:
  si ceci n'est pas possédé, dire "Vous ne possédez pas [intitulé ceci]";
  si cela n'est pas une personne, dire "Je préfère donner ça à quelqu'un.".

exécuter donner ceci à cela:
  déplacer ceci vers cela.

terminer donner ceci à cela:
  dire "Vous avez donné [intitulé ceci] à [intitulé cela].".
  
L'aide pour l'action donner est "{*Donner*}
  Permet de donner un objet que vous possédez à quelqu'un.
  {+exemples+} :
  > {-donner {/la poudre magique/} à {/la magicienne/}-}
  > {-donner {/la pomme/} au {/nain/}-}
  {+raccourci+} : {-d-}".

SCÈNE "jeter".
-- =========
--   JETER
-- =========

-- a) jeter
le joueur peut jeter:
  dire "jeter quoi ?".
  
-- b) jeter ceci
jeter ceci est une action qui concerne un objet visible.

refuser jeter ceci:
  si ceci n'est pas possédé, dire "Vous ne possédez pas [intitulé ceci]".
  
exécuter jeter ceci:
  déplacer ceci vers ici.
  
terminer jeter ceci:
  dire "Vous avez jeté [intitulé ceci]".

L'aide pour l'action jeter est "{*Jeter*}
  Permet de jeter un objet que vous possédez sur le sol.
  {+exemple+} :
  > {-jeter {/la fleur/}-}
  {+raccourci+} : {-j-}".

SCÈNE "mettre".
-- ===========
--   METTRE
-- ===========

-- a) mettre
le joueur peut mettre:
  dire "Je sais poser un objet sur un support ou le mettre dans un contenant.".
  
-- c) metre ceci dans cela
mettre ceci dans cela est une action qui concerne un objet visible et un objet visible.

refuser mettre ceci dans cela:
  si ceci n'est pas accessible, dire "[intitulé ceci] n’est pas accessible.";
  si cela n'est pas accessible, dire "[intitulé cela] n’est pas accessible.";
  si cela n'est ni un support ni un contenant, dire "[intitulé ceci] n’est ni un contenant ni un support".

exécuter mettre ceci dans cela:
  déplacer ceci sur cela.

terminer mettre ceci sur cela:
  dire "C’est fait.".

L'aide pour l'action mettre est "{*Mettre / Poser*}
  Permet de poser un objet sur un support ou de le mettre dans un contenant.
  {+exemples+} :
  > {-mettre {/la pomme/} dans {/le panier/}-}
  > {-poser {/l'épée/} sur {/la table/}-}
  {+raccourci+} : {-me-}, {-po-}".
  
  
